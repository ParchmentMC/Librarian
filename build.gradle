plugins {
    id 'org.parchmentmc.writtenbooks' version '0.+'
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'org.cadixdev.licenser' version '0.6.1'
    id 'groovy'
}

project.group = 'org.parchmentmc'
project.version = neogradle_version

writtenbooks {
    githubRepo = 'ParchmentMC/Librarian'
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(8)
    withSourcesJar()
}

configurations {
    forgegradle
    compileOnly.extendsFrom forgegradle
}

repositories {
    mavenCentral()
    mavenLocal()
    gradlePluginPortal()
    // ForgeGradle dependencies
    maven {
        name = 'NeoForged'
        url = 'https://maven.neoforged.net/releases'
    }
}

dependencies {
    implementation gradleApi()
    api 'org.parchmentmc.feather:io-gson:0.6.5'

    // ForgeGradle
    api (group: 'net.neoforged.gradle', name: 'userdev', version: neogradle_version) { spec ->
        spec.exclude group: 'org.codehaus.groovy'
    }
    api (group: 'net.neoforged.gradle', name: 'dsl-userdev', version: neogradle_version) { spec ->
        spec.exclude group: 'org.codehaus.groovy'
    }
    api (group: 'net.neoforged.gradle', name: 'common', version: neogradle_version) { spec ->
        spec.exclude group: 'org.codehaus.groovy'
    }
    api (group: 'net.neoforged.gradle', name: 'dsl-common', version: neogradle_version) { spec ->
        spec.exclude group: 'org.codehaus.groovy'
    }
    api (group: 'net.neoforged.gradle', name: 'neoform', version: neogradle_version) { spec ->
        spec.exclude group: 'org.codehaus.groovy'
    }
    api (group: 'net.neoforged.gradle', name: 'dsl-neoform', version: neogradle_version) { spec ->
        spec.exclude group: 'org.codehaus.groovy'
    }
    api 'net.minecraftforge:srgutils:0.4.3'
    api 'com.google.guava:guava:30.1-jre'
    api 'com.google.code.gson:gson:2.8.7'
    api 'de.siegmar:fastcsv:2.0.0'

    testImplementation gradleTestKit()
    testImplementation "org.junit.jupiter:junit-jupiter:${project.junit_version}"
    testImplementation("org.spockframework:spock-core:${project.spock_version}-groovy-${project.groovy_version}") { spec ->
        spec.exclude group: 'org.codehaus.groovy'
    }
    testImplementation ("net.neoforged.trainingwheels:trainingwheels-gradle-functional:${project.trainingwheels_version}") { spec ->
        spec.exclude group: 'org.codehaus.groovy'
    }

    testRuntimeOnly("org.junit.platform:junit-platform-launcher")
}

license {
    header = file('HEADER.txt')
    ext {
        name = 'Librarian'
        year = 2021
        fullname = 'ParchmentMC'
    }
    exclude '**/*.properties'
}

gradlePlugin {
    plugins {
        librarianForgeGradle {
            id = 'org.parchmentmc.librarian.neogradle'
            implementationClass = 'org.parchmentmc.librarian.neogradle.LibrarianNeoGradlePlugin'
        }
    }
}

publishing {
    publications {
        pluginMaven(MavenPublication) {
            pom {
                name = 'Librarian'
                description = 'Gradle plugin adding the parchment mappings channel to NeoGradle.'
                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name = 'projectLocal'
            url = "file://${rootProject.file('repo').absolutePath}"
        }
    }
}

tasks.named('test', Test) {
    useJUnitPlatform()
}